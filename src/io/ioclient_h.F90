!>----------------------------------------------------------
!!  Define the interface for the ioclient
!!
!!  The I/O client serves as a parallelized interface between the main program
!!  and the I/O routines. The I/O client handles the buffering and exchange
!!  of data between the processes of the main program (child processes)
!!  and the I/O processes (parent processes)
!! 
!!  @author
!!  Dylan Reynolds (dylan.reynolds@slf.ch)
!!
!!----------------------------------------------------------
module ioclient_interface
  use mpi
  use icar_constants
  use variable_interface, only : variable_t
  use boundary_interface, only : boundary_t
  use domain_interface,   only : domain_t
  use options_interface,  only : options_t

!  use time_object,        only : Time_type, THREESIXTY, GREGORIAN, NOCALENDAR, NOLEAP

  implicit none

  private
  public :: ioclient_t

  ! Pair of variable-name lists used to describe one direction of the
  ! init-only nest transfer (parent -> children or children <- parent).
  ! 2D variables and init-only 3D restart variables live in the same struct
  ! because the two lists are always populated, consumed, and freed together.
  type :: nest_init_var_list_t
      character(len=kMAX_NAME_LENGTH) :: vars_2d(kMAX_STORAGE_VARS) = ''
      character(len=kMAX_NAME_LENGTH) :: vars_3d(kMAX_STORAGE_VARS) = ''
  end type nest_init_var_list_t

  !>----------------------------------------------------------
  !! Output type definition
  !!
  !!----------------------------------------------------------
  type :: ioclient_t

      ! all components are private and should only be modified through procedures
      private

      ! Store the variables to be written
      ! Note n_variables may be smaller then size(variables) so that it doesn't
      ! have to keep reallocating variables whenever something is added or removed
      integer, public :: n_input_variables, n_output_variables
      
      integer, public :: parent_comms

      type(variable_t), public, allocatable :: variables(:)
      ! time variable , publicis stored outside of the variable list... probably need to think about that some
                  
      integer, public :: server
      
      integer, public ::  i_s_w, i_e_w, k_s_w, k_e_w, j_s_w, j_e_w, n_w, i_s_r, i_e_r, k_s_r, k_e_r, j_s_r, j_e_r, n_r, i_s_re, i_e_re, j_s_re, j_e_re, ide, kde, jde
      integer, public :: nz_init_3d = 0
      integer :: restart_counter = 0
      integer :: output_counter = 0
      integer :: frames_per_outfile, restart_count

      ! Restart variable classification counts
      logical :: first_push = .true.
      integer :: vars_for_output(kMAX_STORAGE_VARS) = 0
      integer :: vars_for_restart(kMAX_STORAGE_VARS) = 0

      logical :: written = .False.
      logical :: nest_updated = .False.

      real, dimension(:,:,:,:), pointer :: read_buffer => null()
      real, dimension(:,:,:,:), pointer :: write_buffer_3d => null()
      real, dimension(:,:,:,:), pointer :: forcing_buffer => null()
      real, dimension(:,:,:),   pointer :: write_buffer_2d => null()
      real, dimension(:,:,:),   pointer :: forcing_buffer_2d => null()
      real, dimension(:,:,:,:), pointer :: forcing_buffer_3d_init => null()

      ! MPI vector types for output-only send.
      integer :: send_type_3d_out
      integer :: send_type_2d_out

      ! Outstanding Irecv on read_buffer (kIO_TAG_READ). Posted early in
      ! init_ioclient so the server-side Isend from parent scatter_forcing
      ! during our wake does not race ahead of our reaching receive().
      integer :: read_req

      ! Outstanding Irecvs for restart read (only if options%restart%restart).
      ! Allocated + posted in init_ioclient, waited on in receive_rst.
      integer :: rst_req_3d, rst_req_2d
      real, allocatable :: rst_scratch_3d(:,:,:,:)
      real, allocatable :: rst_scratch_2d(:,:,:)
      logical :: rst_posted = .false.

      character(len=kMAX_NAME_LENGTH) :: vars_for_nest(kMAX_STORAGE_VARS)

      ! Init-only nest transfer: 2D + extra 3D restart vars (not in atmospheric forcing)
      type(nest_init_var_list_t) :: send_init_vars  ! parent -> children (this node is parent)
      type(nest_init_var_list_t) :: recv_init_vars  ! child  <- parent   (this node is child)

      ! Two independent gates for the one-time parent->child init transfer.
      !   nest_init_send_done = .true.  -> skip the parent's init Isend in update_nest
      !                                    (no child of this nest needs the transfer, or
      !                                    we have already sent once).
      !   nest_init_recv_done = .true.  -> skip the child's MPI_Recv + unpack in
      !                                    receive_nest_init (this nest doesn't need
      !                                    a transfer, or we have already received).
      ! Computed in init_ioclient from options(:) using the three-condition rule:
      !   nest needs recv  iff  (.not. restart) .and. (parent_nest > 0)
      !                          .and. (start_time > parent's start_time)
      !   nest needs send  iff  any of its children needs recv.
      ! Also flipped to .true. as a sentinel after the first successful send/recv.
      logical :: nest_init_send_done = .false.
      logical :: nest_init_recv_done = .false.

  contains

      procedure, public  :: push
      procedure, public  :: receive
      procedure, public  :: receive_rst
      procedure, public  :: receive_nest_init
      procedure, public  :: update_nest
      procedure, public  :: init => init_ioclient
  end type

  interface

      !>----------------------------------------------------------
      !! Initialize the object (e.g. allocate the variables array)
      !!
      !!----------------------------------------------------------
    module subroutine init_ioclient(this, domain, forcing, options, n_indx)
        implicit none
        class(ioclient_t),  intent(inout)  :: this
        type(domain_t),     intent(inout)  :: domain
        type(boundary_t),   intent(in)     :: forcing
        type(options_t),    intent(in)     :: options(:)
        integer,            intent(in)     :: n_indx
    end subroutine init_ioclient

      !>----------------------------------------------------------
      !! Push output data to IO buffer
      !!
      !!----------------------------------------------------------
      module subroutine push(this, domain)
          implicit none
          class(ioclient_t),   intent(inout) :: this
          type(domain_t),   intent(inout)    :: domain
      end subroutine

      !>----------------------------------------------------------
      !! Receive input data
      !!
      !!----------------------------------------------------------
      module subroutine receive(this, forcing, domain)
          implicit none
          class(ioclient_t), intent(inout) :: this
          type(boundary_t), intent(inout)  :: forcing
          type(domain_t),   intent(inout)  :: domain
      end subroutine

      !>----------------------------------------------------------
      !! Receive restart data
      !!
      !!----------------------------------------------------------
      module subroutine receive_rst(this, domain, options)
          implicit none
          class(ioclient_t), intent(inout) :: this
          type(domain_t),   intent(inout)  :: domain
          type(options_t),  intent(in)     :: options

      end subroutine

        !>----------------------------------------------------------
        !! Receive initial 2D+3D restart state from parent nest
        !! (one-time transfer at child wake-up)
        !!
        !!----------------------------------------------------------
        module subroutine receive_nest_init(this, domain, forcing)
            implicit none
            class(ioclient_t), intent(inout) :: this
            type(domain_t),    intent(inout) :: domain
            type(boundary_t),  intent(in)    :: forcing
        end subroutine

        !>----------------------------------------------------------
        !! Update the nest
        !!
        !!----------------------------------------------------------
        module subroutine update_nest(this, domain, options)
            implicit none
            class(ioclient_t), intent(inout) :: this
            type(domain_t),    intent(in) :: domain
            type(options_t),   intent(in) :: options
        end subroutine

  end interface
end module
