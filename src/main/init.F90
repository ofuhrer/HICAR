!> ----------------------------------------------------------------------------
!!  Model Initialization includes allocating memory for boundary and domain
!!      data structures.  It reads all of the options from the namelist
!!      file (or files).  It also reads in Lat/Lon and Terrain data.  This module
!!      also sets up geographic (and vertical) look uptables for the forcing data
!!      Finally, there is a driver routine to initialize all model physics packages
!!
!!   The module has been updated to allow arbitrary named variables
!!       this allows the use of e.g. ERAi, but still is not as flexible as it could be
!!
!!   The use of various python wrapper scripts in helpers/ makes it easy to add new
!!       datasets, and make them conform to the expectations of the current system.
!!      For now there are no plans to near term plans to substantially modify this.
!!
!!  @author
!!  Ethan Gutmann (gutmann@ucar.edu)
!!
!! ----------------------------------------------------------------------------
submodule(initialization) initialization_implementation
    use namelist_utils,     only : inter_nest_namelist_check
    use microphysics,               only : mp_init, mp_var_request
    use advection,                  only : adv_init, adv_var_request
    use radiation,                  only : radiation_init, ra_var_request
    use convection,                 only : init_convection, cu_var_request
    use planetary_boundary_layer,   only : pbl_init, pbl_var_request
    use land_surface,               only : lsm_init, lsm_var_request
    use surface_layer,              only : sfc_init, sfc_var_request
    use wind,                       only : wind_var_request
    use hicar_initialization_core,  only : initialize_atmospheric_winds
    use io_routines,                only : io_newunit
    use omp_lib,                    only : omp_get_max_threads
    use icar_constants
    use ioserver_interface,         only : ioserver_t
    use iso_fortran_env,            only : output_unit
    use mpi
#ifdef _OPENACC
    use openacc
#endif


    implicit none

contains


    module subroutine split_processes(components, ioclient, n_nests, options)
        implicit none
        type(comp_arr_t), intent(inout) :: components(:)
        type(ioclient_t), intent(inout) :: ioclient(:)
        integer, intent(in) :: n_nests
        type(options_t), intent(in) :: options

        integer :: n, k, name_len, color, IOcolor, ierr, num_PE
        integer :: num_threads, found, PE_RANK_GLOBAL, NUM_SERVERS, NUM_COMPUTE, NUM_IO_PER_NODE, NUM_PROC_PER_NODE
        character(len=MPI_MAX_PROCESSOR_NAME) :: ENV_IO_PER_NODE
        integer :: globalComm, shared_comm, mainComms, IOComms
        integer :: host_only_info

#ifdef _OPENACC
        integer :: dev, local_rank, comm_size, NUM_GPU_PER_NODE
        integer :: local_comm
        integer(acc_device_kind) :: devtype
#endif

#if defined(_OPENMP)
        num_threads = omp_get_max_threads()
#else
        num_threads = 1
#endif    

        ! See if the environment variable HICAR_IO_PER_NODE is set
        ! If it is, then we will use that to determine the number of IO processes per node
        call get_environment_variable('HICAR_IO_PER_NODE',ENV_IO_PER_NODE,found)

        ! If the environment variable is not set, then we will use 1
        if (found == 0) then
            NUM_IO_PER_NODE = 1
        else
            ! If the number of IO processes per node is set, then we will use that
            read(ENV_IO_PER_NODE,*) NUM_IO_PER_NODE
        endif

        call MPI_Comm_Size(MPI_COMM_WORLD,num_PE, ierr)
        call MPI_Comm_Rank(MPI_COMM_WORLD,PE_RANK_GLOBAL, ierr)

        !Discover the number of processors per node
        CALL MPI_Comm_dup( MPI_COMM_WORLD, globalComm, ierr )
        call MPI_Comm_split_type(globalComm, MPI_COMM_TYPE_SHARED, 0, MPI_INFO_NULL, shared_comm, ierr)
        call MPI_Comm_Size(shared_comm,NUM_PROC_PER_NODE, ierr)
        call MPI_Comm_free(shared_comm, ierr)
        ! call MPI_Comm_free(globalComm, ierr)


        !limit NUM_IO_PER_NODE to be less than or equal to NUM_PROC_PER_NODE
        if (NUM_IO_PER_NODE > NUM_PROC_PER_NODE) then
            NUM_IO_PER_NODE = NUM_PROC_PER_NODE
            if (STD_OUT_PE) write(*,*) "WARNING: NUM_IO_PER_NODE was set greater than the number of processing elements per node. Setting NUM_IO_PER_NODE to ",NUM_IO_PER_NODE
        endif

        !Ensure that NUM_PROC_PER_NODE is evenly divisible by NUM_IO_PER_NODE. If not, decrement
        ! NUM_IO_PER_NODE until this is true
        if (mod(NUM_PROC_PER_NODE, NUM_IO_PER_NODE) /= 0) then
            do while (mod(NUM_PROC_PER_NODE, NUM_IO_PER_NODE) /= 0)
                NUM_IO_PER_NODE = NUM_IO_PER_NODE - 1
                if (NUM_IO_PER_NODE < 1) then
                    write(*,*) "ERROR: NUM_IO_PER_NODE has been reduced to zero."
                    stop
                endif
            end do
            if (STD_OUT_PE) write(*,*) "WARNING: NUM_PROC_PER_NODE was not evenly divisible by NUM_IO_PER_NODE. Setting NUM_IO_PER_NODE to ",NUM_IO_PER_NODE
        endif

        !Assign one io process per node, this results in best co-array transfer times
        NUM_SERVERS = ceiling(num_PE*NUM_IO_PER_NODE*1.0/NUM_PROC_PER_NODE)
        NUM_COMPUTE = num_PE-NUM_SERVERS

        ! HICAR reserves at least one rank per node for asynchronous I/O, so a run
        ! needs more ranks than I/O servers. If NUM_COMPUTE is zero (e.g. launched
        ! with a single MPI rank, or with only one rank per node) there are no
        ! compute tasks and the model cannot run. Fail here with a clear, explicit
        ! message and MPI_Abort the whole job, rather than deadlocking or dying with
        ! a cryptic error deeper in initialization.
        if (NUM_COMPUTE < 1) then
            if (STD_OUT_PE) then
                write(*,*) "-------------------------------------------------------"
                write(*,*) "ERROR: HICAR has no compute processes to run on."
                write(*,*) "  Total MPI ranks (num_PE):  ", num_PE
                write(*,*) "  Ranks reserved for I/O:    ", NUM_SERVERS
                write(*,*) "  Remaining compute ranks:   ", NUM_COMPUTE
                write(*,*) " "
                write(*,*) "HICAR reserves at least one rank per node for asynchronous I/O,"
                write(*,*) "so it must be launched with more ranks than I/O servers (i.e. at"
                write(*,*) "least one compute rank). On a single node this means at least 2"
                write(*,*) "MPI ranks, e.g.:  mpiexec -np 2 ./bin/HICAR your_namelist.nml"
                write(*,*) "The number of I/O processes per node can be controlled with"
                write(*,*) "the environment variable HICAR_IO_PER_NODE."
                write(*,*) "-------------------------------------------------------"
                flush(output_unit)
            endif
            call MPI_Abort(MPI_COMM_WORLD, 1, ierr)
        endif

        STD_OUT_PE_IO = (PE_RANK_GLOBAL == ((NUM_PROC_PER_NODE/NUM_IO_PER_NODE)-1) ) .and. (options%general%debug .or. STD_OUT_PE_IO)
        
        if ((mod(NUM_COMPUTE,2) /= 0) .and. STD_OUT_PE) then
            write(*,*) 'WARNING: number of compute processes is odd-numbered.' 
            write(*,*) 'One process per node is used for I/O.'
            write(*,*) 'If the total number of compute processes is odd-numbered,'
            write(*,*) 'this may lead to errors with domain decomposition'
            flush(output_unit)
        endif

        !-----------------------------------------
        ! Assign Compute and IO processes
        !-----------------------------------------
        if (mod((PE_RANK_GLOBAL+1),(num_PE/NUM_SERVERS)) == 0) then
            color = kIO_TEAM
        else
            color = kCOMPUTE_TEAM
        endif

        ! Assign all images in the IO team to the IO_comms MPI communicator. Use image indexing within initial team to get indexing of global MPI ranks
        CALL MPI_Comm_split( globalComm, color, PE_RANK_GLOBAL, mainComms, ierr )

        ! Assign IO clients to the same communicator as their related server process
        IOcolor = (PE_RANK_GLOBAL) / (num_PE/NUM_SERVERS)

        ! Group IO clients with their related server process. This is basically just grouping processes by node
        CALL MPI_Comm_split( globalComm, IOcolor, PE_RANK_GLOBAL, IOComms, ierr )

        do n = 1, n_nests
            if (color == kIO_TEAM) then
                allocate(ioserver_t::components(n)%comp)
            elseif (color == kCOMPUTE_TEAM) then
                allocate(domain_t::components(n)%comp)
            endif
        enddo

        ! MPI-4.1 hint: the IO comms only carry host buffers, so MPI can skip
        ! the CUDA pointer-attribute probe on Isend/Irecv and on HDF5's
        ! internal collectives. compute_comms is deliberately left untouched
        ! because halo%exch_var passes device pointers on GPU builds
        ! (see halo_obj.F90 `!$acc host_data use_device`). Ignored silently
        ! by OpenMPI < 5.0.
        call MPI_Info_create(host_only_info, ierr)
        call MPI_Info_set(host_only_info, "mpi_memory_alloc_kinds", "system", ierr)

        do n = 1, n_nests
            associate(comp => components(n)%comp)
            select type (comp)
                type is (domain_t)
                    CALL MPI_Comm_dup( mainComms, comp%compute_comms, ierr )
                    CALL MPI_Comm_dup( IOComms, ioclient(n)%parent_comms, ierr )
                    call MPI_Comm_set_info(ioclient(n)%parent_comms, host_only_info, ierr)

                type is (ioserver_t)
                    CALL MPI_Comm_dup( mainComms, comp%IO_comms, ierr )
                    CALL MPI_Comm_dup( IOComms, comp%client_comms, ierr )
                    call MPI_Comm_set_info(comp%IO_comms,     host_only_info, ierr)
                    call MPI_Comm_set_info(comp%client_comms, host_only_info, ierr)

                class default
                    ! some default behavior
            end select
            end associate
        enddo

        call MPI_Info_free(host_only_info, ierr)

#ifdef _OPENACC
    !
    ! ****** Set the Accelerator device number based on local rank
    !
    call MPI_Comm_split_type(MPI_COMM_WORLD, MPI_COMM_TYPE_SHARED, 0, &
        MPI_INFO_NULL, local_comm, ierr)
    call MPI_Comm_rank(local_comm, local_rank, ierr)
    call MPI_Comm_size(local_comm, comm_size, ierr)
    
    if (color == kCOMPUTE_TEAM) then
        call acc_set_device_type(acc_device_nvidia)
        devtype = acc_get_device_type()
        NUM_GPU_PER_NODE = acc_get_num_devices(devtype)

        !get our local rank, not counting IO processes
        local_rank = local_rank - floor(1.0*local_rank/(comm_size/NUM_IO_PER_NODE))
        !divy up GPUs according to local rank
        dev = mod(local_rank,NUM_GPU_PER_NODE)

        call acc_set_device_num(dev, devtype)
        call acc_init(devtype)
    endif
# endif

        if (STD_OUT_PE) then
            write(*,*) "  Number of processing elements:          ",num_PE
            write(*,*) "  Number of compute elements:             ",NUM_COMPUTE
            write(*,*) "  Number of IO elements:                  ",NUM_SERVERS
            write(*,*) "  Number of processing elements per node: ",NUM_PROC_PER_NODE
            write(*,*) "  Number of IO processes per node:        ",NUM_IO_PER_NODE
#if defined(_OPENACC)
            write(*,*) "  Number of GPUs used per node: ", NUM_GPU_PER_NODE
#endif

#if defined(_OPENMP)
            write(*,*) "  Max number of OpenMP Threads:           ",num_threads
#endif
            flush(output_unit)

        endif

    end subroutine split_processes

    module subroutine init_options(options, namelist_file, info_only, gen_nml, only_namelist_check)
        implicit none
        type(options_t), allocatable, intent(out) :: options(:)
        character(len=*), intent(in) :: namelist_file
        logical, intent(in), optional :: info_only, gen_nml, only_namelist_check

        logical :: info = .False.
        logical :: gennml = .False.
        logical :: nml_check = .False.
        integer :: num_nests, i, rc, name_unit, io_stat, equals_pos
        character(len=200) :: line, text

        if (present(info_only)) info = info_only
        if (present(gen_nml)) gennml = gen_nml
        if (present(only_namelist_check)) nml_check = only_namelist_check

        if (STD_OUT_PE .and. .not.(gennml .or. nml_check .or. info)) then
            call welcome_message()
            write(*,'(/ A)') "--------------------------------------------------------"
            write(*,'(A)')   "Initializing Options"
            write(*,'(A /)') "--------------------------------------------------------"
            flush(output_unit)
        endif


        ! We need at least one domain
        num_nests = 1

        ! If we are generating or printing namelist option information, we don't want to read anything, and only need to run the init routine once
        ! First thing, read from options file how many nests we have
        if ( .not.(info .or. gennml) )then
            open(io_newunit(name_unit), file=namelist_file, status='old')
            do
                read(name_unit, '(A)', iostat=io_stat) line
                if (io_stat /= 0) exit

                text = adjustl(line)
                ! Look for the variable nests in the line
                if (text(1:6) == "nests ") then
                    equals_pos = index(line, "=")
                    if (equals_pos > 0) then
                        read(line(equals_pos+1:), *) num_nests
                        exit
                    endif
                endif
            end do
            close(name_unit)
        endif

        allocate(options(num_nests))

        ! read in options file
        do i = 1, num_nests
            call options(i)%init(namelist_file, i, info_only=info, gen_nml=gennml)
            call collect_physics_requests(options(i))

            if (options(i)%general%parent_nest > 0) then
                ! Setup options%forcing to expect synthetic forcing from parent domain
                call options(i)%setup_synthetic_forcing(options(options(i)%general%parent_nest))
            endif
            call options(i)%verify_options()

        enddo

        call inter_nest_namelist_check(options)

        ! If this run was just done to check the namelist options, stop now
        if (nml_check) then
            if (STD_OUT_PE) write(*,*) 'Namelist options check complete.'
            stop
        endif

    end subroutine init_options

    module subroutine init_model(options,domain,boundary,ioclient,nest_indx)
        implicit none
        type(options_t), intent(inout) :: options(:)
        type(domain_t),  intent(inout) :: domain
        type(boundary_t),intent(inout) :: boundary ! forcing file for init conditions
        type(ioclient_t),intent(inout) :: ioclient
        integer, intent(in) :: nest_indx
        
        if (STD_OUT_PE) write(*,*) "Initializing Domain ", nest_indx
        if (STD_OUT_PE) flush(output_unit)
        call domain%init(options(nest_indx), nest_indx)

        if (options(nest_indx)%general%parent_nest == 0) then
            if (STD_OUT_PE) write(*,*) "Initializing boundary condition data structure"
            if (STD_OUT_PE) flush(output_unit)    
            call boundary%init(options(nest_indx), domain%vars_2d(domain%var_indx(kVARS%latitude)%v)%data_2d, domain%vars_2d(domain%var_indx(kVARS%longitude)%v)%data_2d)
        else
            call boundary%init(options(nest_indx), domain%vars_2d(domain%var_indx(kVARS%latitude)%v)%data_2d, domain%vars_2d(domain%var_indx(kVARS%longitude)%v)%data_2d, &
                                parent_options=options(options(nest_indx)%general%parent_nest))
        endif
        if (STD_OUT_PE) write(*,*) "Initializing IO client"
        if (STD_OUT_PE) flush(output_unit)    

        call ioclient%init(domain, boundary, options, nest_indx)

    end subroutine init_model

    module subroutine init_model_state(options, domain, boundary, ioclient)
        implicit none
        type(options_t), intent(inout) :: options
        type(domain_t),  intent(inout) :: domain
        type(boundary_t),intent(inout) :: boundary ! forcing file for init conditions
        type(ioclient_t),intent(inout) :: ioclient

        integer :: comm_size, ierr

        if (STD_OUT_PE) write(*,*) "Receiving initial data"
        if (STD_OUT_PE) flush(output_unit)
        call ioclient%receive(boundary, domain)

        if (STD_OUT_PE) write(*,*) "Populating boundary object"
        if (STD_OUT_PE) flush(output_unit)
        call boundary%update_computed_vars(options)

        if (STD_OUT_PE) write(*,*) "Initializing forcing interpolation"
        call domain%get_initial_conditions(boundary, options)

        ! Receive full state from parent nest (one-time, with geo-interpolation).
        ! The three-condition gate (no restart, has parent, start_time > parent.start_time)
        ! is enforced inside receive_nest_init via nest_init_recv_done; this call is a
        ! cheap no-op when the gate is set.
        call ioclient%receive_nest_init(domain, boundary)

        ! now read in any static land data. This way we do: 
        ! 1) init via default values, 
        ! 2) receive any parent state variables from a spin-up,
        ! 3) any user-set variables get the last word
        call domain%read_land_variables(options)

        if (options%restart%restart) then
            if (STD_OUT_PE) write(*,*) "Reading restart data"
            call ioclient%receive_rst(domain, options)
            ! Bcast restart_dt here, BEFORE update_nest below. Otherwise the
            ! client's update_nest Isend+Wait crosses the server's bcast in
            ! wake_component (which sits ahead of the matching Irecv in
            ! update_component_nest), and the two ranks deadlock.
            call MPI_Comm_Size(ioclient%parent_comms, comm_size, ierr)
            call MPI_Bcast(domain%restart_dt, 1, MPI_REAL, comm_size-1, &
                           ioclient%parent_comms, ierr)
            call MPI_Bcast(domain%adv_theta_ref_n, 1, MPI_INTEGER, comm_size-1, &
                           ioclient%parent_comms, ierr)
            if (domain%adv_theta_ref_n <= 0 .or. domain%adv_theta_ref_n > MAXLEVELS) then
                error stop "Invalid advection theta reference profile count on restart"
            endif
            call MPI_Bcast(domain%adv_theta_ref_z, domain%adv_theta_ref_n, MPI_REAL, &
                           comm_size-1, ioclient%parent_comms, ierr)
            call MPI_Bcast(domain%adv_theta_ref_theta, domain%adv_theta_ref_n, MPI_REAL, &
                           comm_size-1, ioclient%parent_comms, ierr)
        endif

        if (STD_OUT_PE) write(*,*) "Initializing physics"
        ! physics drivers need to be initialized after restart data are potentially read in.
        call init_physics(options, domain)
        if (STD_OUT_PE) flush(output_unit)

        if (.not.(options%restart%restart)) then
            if (domain%var_indx(kVARS%wind_u_agl)%v > 0) then
                call domain%diagnostic_update()
                call domain%update_wind_height_diagnostics()
            endif
            call ioclient%push(domain)
        endif


    end subroutine init_model_state


    module subroutine init_physics(options, domain)
        implicit none
        type(options_t), intent(inout) :: options
        type(domain_t),  intent(inout) :: domain


        if (STD_OUT_PE .and. options%restart%restart) then
            write(*,*) "Initializing wind solver without altering restart winds"
        else if (STD_OUT_PE) then
            write(*,*) "Initializing and projecting initial winds"
        endif
        if (STD_OUT_PE) flush(output_unit)
        call initialize_atmospheric_winds(&
            domain, options, project_state=.not. options%restart%restart)

        ! initialize microphysics code (e.g. compute look up tables in Thompson et al)
        call mp_init(domain, options) !this could easily be moved to init_model...
        if (STD_OUT_PE) flush(output_unit)

        call init_convection(domain,options)
        if (STD_OUT_PE) flush(output_unit)

        call pbl_init(domain,options)
        if (STD_OUT_PE) flush(output_unit)

        call radiation_init(domain,options)
        if (STD_OUT_PE) flush(output_unit)

        call lsm_init(domain,options)
        if (STD_OUT_PE) flush(output_unit)

        call sfc_init(domain,options)
        if (STD_OUT_PE) flush(output_unit)

        call adv_init(domain,options)
        if (STD_OUT_PE) flush(output_unit)

    end subroutine init_physics


    subroutine welcome_message()
        implicit none

        character(len=38) :: tag_field, commit_field

        tag_field    = GIT_TAG
        commit_field = GIT_COMMIT

        write(*,*) ""
        write(*,*) "============================================================"
        write(*,*) "|                                                          |"
        write(*,*) "|  The Intermediate Complexity Atmospheric Research Model  |"
        write(*,*) "|                       (ICAR/HICAR)                       |"
        write(*,*) "|                                                          |"
        write(*,*) "|   ICAR Developed at NCAR:                                |"
        write(*,*) "|     The National Center for Atmospheric Research         |"
        write(*,*) "|     NCAR is sponsored by the National Science Foundation |"
        write(*,*) "|   HICAR Developed at SLF:                                |"
        write(*,*) "|     The Swiss Institute for Snow and Avalanche Research  |"
        write(*,*) "|                                                          |"
        write(*,*) "|   Version (tag): " // tag_field // "  |"
        write(*,*) "|   Git commit:    " // commit_field // "  |"
        write(*,*) "|                                                          |"
        write(*,*) "============================================================"
        write(*,*) ""

    end subroutine welcome_message

    !> -------------------------------
    !! Call all physics driver var_request routines
    !!
    !! var_request routines allow physics modules to requested
    !! which variables they need to have allocated, advected, and written in restart files
    !!
    !! -------------------------------
    subroutine collect_physics_requests(options)
        type(options_t), intent(inout) :: options

        call ra_var_request(options)
        call lsm_var_request(options)
        call sfc_var_request(options)
        call pbl_var_request(options)
        call cu_var_request(options)
        call mp_var_request(options)
        call adv_var_request(options)
        call wind_var_request(options)

    end subroutine collect_physics_requests

end submodule initialization_implementation
